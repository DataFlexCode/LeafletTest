Use cGeocodeConnector.pkg

Class cMapQuestOpenConnector is a cGeocodeConnector
    
    Procedure Construct_Object
        Forward Send Construct_Object

        Set psHost      to "open.mapquestapi.com"
        Set psPath      to "geocoding/v1/address"
        Set psKey       to (Config("tokens.mapQuestOpenKey"))
        Set psName      to "Open MapQuest"
        Set psID        to "OMAPQ"
        Set psWebSite   to "https://developer.mapquest.com/"
    End_Procedure
    
    Function FreeQuery String sQuery Returns Handle
        Handle hoJson
        
        Move (Replaces(" ", sQuery, "%20")) to sQuery
        Move ("location=" + sQuery)         to sQuery
        Get DoGeoQuery sQuery               to hoJson
        
        Function_Return hoJson
    End_Function
    
    Function StructuredQuery String[] asParams Returns Handle
        String[] asQuery
        String   sQuery
        Handle   hoJson
        Integer  i iMax
        
        Move (SizeOfArray(asParams) - 1) to iMax
        
        For i from 0 to iMax
            Move (Replaces(" ", asParams[i], "+")) to asParams[i]
        Loop        

        If (asParams[0] <> "") ;
            Move ("country=" + asParams[0])                 to asQuery[SizeOfArray(asQuery)]
        If (asParams[1] <> "") ;
            Move ("postalCode=" + asParams[1])              to asQuery[SizeOfArray(asQuery)]
        If (asParams[2] <> "") ;
            Move ("state=" + asParams[2])                   to asQuery[SizeOfArray(asQuery)]
        If (asParams[3] <> "") ;
            Move ("county=" + asParams[3])                  to asQuery[SizeOfArray(asQuery)]
        If (asParams[4] <> "") ;
            Move ("city=" + asParams[4])                    to asQuery[SizeOfArray(asQuery)]
        If (asParams[6] <> "") ;
            Move (asParams[6] * asParams[5])                to asParams[5]
        If (asParams[5] <> "") ;
            Move ("street=" + asParams[5])                  to asQuery[SizeOfArray(asQuery)]
        
        Move (StrJoinFromArray(asQuery, "&"))   to sQuery
        Move (Replaces(" ", sQuery, "%20"))     to sQuery
        Get DoGeoQuery sQuery to hoJson
        
        Function_Return hoJson
    End_Function
    
    Function PostcodeQuery String sCountry String sPostcode Returns Handle
        String sQuery
        Handle hoJson
        
        Move ("postalCode=" + sPostcode + "&" + "country=" + sCountry) to sQuery
        Move (Replaces(" ", sQuery, "%20")) to sQuery
        Get DoGeoQuery sQuery to hoJson
        
        Function_Return hoJson
    End_Function
    
    Procedure ExtractData Handle hoJson
        Number nLat nLng
        Handle hoBox
        Number[] anBox
        String sVal sAddr
        
        Set pnLat to (ValueAtPath(oJPath(Self), hoJson, "results[0].locations[0].latLng.lat"))
        Set pnLng to (ValueAtPath(oJPath(Self), hoJson, "results[0].locations[0].latLng.lng"))
        
        Move (ValueAtPath(oJPath(Self), hoJson, "results[0].locations[0].street")) to sVal            
        If (sVal <> "") ;
            Move (sAddr + If((Length(sAddr)), ", ", "") + sVal) to sAddr
        Move (ValueAtPath(oJPath(Self), hoJson, "results[0].locations[0].adminArea6")) to sVal
        If (sVal <> "") ;
            Move (sAddr + If((Length(sAddr)), ", ", "") + sVal) to sAddr
        Move (ValueAtPath(oJPath(Self), hoJson, "results[0].locations[0].adminArea5")) to sVal
        If (sVal <> "") ;
            Move (sAddr + If((Length(sAddr)), ", ", "") + sVal) to sAddr
        Move (ValueAtPath(oJPath(Self), hoJson, "results[0].locations[0].adminArea4")) to sVal
        If (sVal <> "") ;
            Move (sAddr + If((Length(sAddr)), ", ", "") + sVal) to sAddr
        Move (ValueAtPath(oJPath(Self), hoJson, "results[0].locations[0].adminArea3")) to sVal
        If (sVal <> "") ;
            Move (sAddr + If((Length(sAddr)), ", ", "") + sVal) to sAddr
        Move (ValueAtPath(oJPath(Self), hoJson, "results[0].locations[0].adminArea2")) to sVal
        If (sVal <> "") ;
            Move (sAddr + If((Length(sAddr)), ", ", "") + sVal) to sAddr
        Move (ValueAtPath(oJPath(Self), hoJson, "results[0].locations[0].postalCode")) to sVal
        If (sVal <> "") ;
            Move (sAddr + If((Length(sAddr)), ", ", "") + sVal) to sAddr
        Move (ValueAtPath(oJPath(Self), hoJson, "results[0].locations[0].adminArea1")) to sVal
        If (sVal <> "") ;
            Move (sAddr + If((Length(sAddr)), ", ", "") + sVal) to sAddr
        
        Set psDisplayName to sAddr
    End_Procedure
    
    Procedure DrawMap Handle hoJson Handle hoMap
        Number[]  anBox
        stPolygon tBox
        String    sName
        
        If not hoMap ;
            Procedure_Return
        If (pnLat(Self) = 100) ;
            Procedure_Return
        
        Forward Send DrawMap hoJson hoMap

        Send DrawMap of hoMap (pnLat(Self)) (pnLng(Self)) (piZoom(Self)) (piMaxZoom(Self))
    End_Procedure

End_Class