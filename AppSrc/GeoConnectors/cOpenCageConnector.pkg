Use cGeocodeConnector.pkg

Class cOpenCageConnector is a cGeocodeConnector
    
    Procedure Construct_Object
        Forward Send Construct_Object
        Set psHost      to "api.opencagedata.com"
        Set psPath      to "geocode/v1/json"
        Set psKey       to (Config("tokens.openCageKey"))
        Set psName      to "OpenCage"
        Set psID        to "OPENCAGE"
        Set psWebSite   to "https://opencagedata.com/"
    End_Procedure
    
    Function FreeQuery String sQuery Returns Handle
        Handle  hoJson
        Boolean bOK
        
        Move (Replaces(" ", sQuery, "%20")) to sQuery
        Move (Replaces(",", sQuery, "%2C")) to sQuery
        
        Move ("q=" + sQuery)    to sQuery
        Get DoGeoQuery sQuery   to hoJson
        
        Function_Return hoJson
    End_Function
    
    Function PostcodeQuery String sCountry String sPostcode Returns Handle
        String  sQuery
        Handle  hoJson
        
        Move ("q=" + sPostcode + "&countrycode=" + sCountry)  to sQuery
        Move (Replaces(" ", sQuery, "%20"))                   to sQuery
        Get DoGeoQuery sQuery                                 to hoJson
        
        Function_Return hoJson
    End_Function
    
    Procedure ExtractData Handle hoJson
        Number nLat nLng
        Handle hoBox
        Number[] anBox
        
        Set pnLat to (ValueAtPath(oJPath(Self), hoJson, "results.[0].geometry.lat"))
        Set pnLng to (ValueAtPath(oJPath(Self), hoJson, "results.[0].geometry.lng"))

        Set psDisplayName to (ValueAtPath(oJPath(Self), hoJson, "results.[0].formatted_address"))
    End_Procedure
    
    Procedure DrawMap Handle hoJson Handle hoMap
        Number[]  anBox
        stPolygon tBox
        String    sName
        
        If not hoMap ;
            Procedure_Return
        If (pnLat(Self) = 100) ;
            Procedure_Return
        
        Forward Send DrawMap hoJson hoMap

        Send DrawMap of hoMap (pnLat(Self)) (pnLng(Self)) (piZoom(Self)) (piMaxZoom(Self))
    End_Procedure
    
End_Class